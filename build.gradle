group 'ServerRequests'

buildscript {
    ext.kotlin_version = '1.1.1'
    repositories {
        mavenCentral()
        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

// create an auxiliary file for the MockMaker of the Mockito 2 framework.
// see http://hadihariri.com/2016/10/04/Mocking-Kotlin-With-Mockito/
task createTestResources << {
    def separator = File.separator
    def fileName = [projectDir, 'src', 'test', 'resources', 'mockito-extensions', 'org.mockito.plugins.MockMaker'].join(separator)
    def mockMakerFile = new File(fileName)
    if (mockMakerFile.exists()) {
        logger.info("File ${mockMakerFile.name} already exists.")
    } else {
        def content = System.env.MOCK_MAKER ?: "mock-maker-inline"
        mockMakerFile.parentFile.mkdirs()
        mockMakerFile.createNewFile()
        mockMakerFile.write(content)
        println "create file ${mockMakerFile.name}"
    }

}
repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlin-eap-1.1"
    }
}
apply plugin: 'idea'
apply plugin: "application"
apply plugin: "java"
apply plugin: "kotlin"
mainClassName = 'serverrequests.MainKt'

sourceSets {
    main.java.srcDirs += 'src/main/'
    test.java.srcDirs += 'src/test/'
}

jar {
    version = '0.0.1'
    manifest {
        attributes 'Main-Class': 'com.veontomo.htmlvalidator.MainKt'
    }
    // This line of code recursively collects and copies all of a project's files
    // and adds them to the JAR itself. One can extend this task, to skip certain
    // files or particular types at will
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.jsoup:jsoup:1.10.1'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.5'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'

    compile 'io.reactivex:rxjava:1.1.6'
    compile 'io.reactivex:rxkotlin:0.60.0'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "org.mockito:mockito-core:2.5.0"
    testCompile 'com.nhaarman:mockito-kotlin:1.1.0'
    testCompile 'de.jodamob.kotlin:kotlin-runner-junit4:0.3.1'
    testCompile "org.testfx:testfx-core:4.0.+"
    testCompile "org.testfx:testfx-junit:4.0.+"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.4-beta'
}
apply from: "script-git-version.gradle"

